plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

group 'com.vdda'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile (
            'com.amazonaws:aws-lambda-java-core:1.1.0',
            'com.amazonaws:aws-lambda-java-events:1.1.0',
            'com.mashape.unirest:unirest-java:1.4.9',
            'org.slf4j:slf4j-simple:1.6.1'
    )
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildZip

task createStack(type: Exec) {
    workingDir '/aws'

    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'bash', 'create_stack.sh' // I'm running this with git bash on windows
    } else {
        commandLine './create_stack.sh'
    }
}

task deleteStack(type: Exec) {
    workingDir '/aws'

    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'bash', 'delete_stack.sh' // I'm running this with git bash on windows
    } else {
        commandLine './delete_stack.sh'
    }
}
